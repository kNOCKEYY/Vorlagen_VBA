Sub ResetSpecificMenu()

' Resets the a specific Shortcutmenu to its normal state
' Adjust "Cell" if other Shortcutmenu is meant to be resetted

    Dim cbar As CommandBar
   
    For Each cbar In Application.CommandBars
        If cbar.Name = "Cell" Then cbar.Reset
    Next cbar

End Sub

Sub ResetAllShortcutMenus()

' Resets all Shortcutmenus in the active window to its normal state

    Dim cbar As CommandBar
    
    For Each cbar In Application.CommandBars
        If cbar.Type = msoBarTypePopup Then
            cbar.Reset
            cbar.Enabled = True
        End If
    Next cbar

End Sub

Sub ResetAllShortMenusInOpenWindows()

' resets all Shortcutmenus in all windows to its normal state

    Dim cbar As CommandBar
    Dim ActiveWin As Window
    Dim win As Window
    
    ' remember current active window
    Set ActiveWin = ActiveWindow
    
    ' Loop through each visible window
    Application.ScreenUpdating = False
    
    For Each win In Windows
        If win.Visible Then
            win.Activate
            For Each cbar In Application.CommandBars
                If cbar.Type = msoBarTypePopup Then
                    cbar.Reset
                    cbar.Enabled = True
                End If
            Next cbar
        End If
    Next win
    
    ' Activate original window
    ActiveWin.Activate
    Application.ScreenUpdating = True
                
End Sub