Private Function GetValue(path, file, sheet, ref)

' Retrieves a value from a closed workbook

Dim arg As String

    ' Make sure the file exists
    If Right(path, 1) <> "\" Then path = path & "\"
    If Dir(path & file) = "" Then
        GetValue = "File Not Found"
        Exit Function
    End If
    
    ' Create the argument
    arg = "'" & path & "[" & file & "]" & sheet & "'!" & _
        Range(ref).Range("A1").Address(, , xlR1C1)
        
    ' Execute an XLM macro
    GetValue = ExecuteExcel4Macro(arg)
        
End Function


Sub TestGetValue()

Dim p As String
Dim f As String
Dim s As String
Dim a As String
Dim r As Long
Dim c As Long

    p = "D:\Excel\Excel 2019 Power Programming\Full book\514923c07samples_022719\batch processing"
    f = "batch processing.xlsm"
    s = "Tabelle1"
    
    Application.ScreenUpdating = False
    
    For r = 1 To 3 ' 100 rows
        For c = 1 To 2 ' 12 columns
            a = Cells(r, c).Address
            Cells(r, c) = GetValue(p, f, s, a)
        Next c
    Next r
    
End Sub
