' VBA InputBox

Sub GetName()

    Dim UserName As String
    Dim FirstSpace As Long
    
    Do Until Len(UserName) > 0
        UserName = InputBox("Enter your full name: ", _
            "Identify Yourself")
    Loop
    
    FirstSpace = InStr(UserName, Space(1))
    If FirstSpace > 0 Then
        UserName = Left$(UserName, FirstSpace - 1)
    End If
    
    MsgBox "Hello " & UserName

End Sub

Sub GetNameSplit()

    Dim UserName As String
    
    Do Until Len(UserName) > 0
        UserName = InputBox("Enter your full name: ", _
            "Identify Yourself", Application.UserName)
    Loop
    
    MsgBox "Hello " & Split(UserName, Space(1))(0)

End Sub

Sub GetWord()

    Dim TheWord As String
    Dim Prompt As String
    Dim Title As String
    
    Prompt = Range("A1").Value
    Title = "What's the missing word?"
    TheWord = InputBox(Prompt:=Prompt, Title:=Title)
    
    If UCase(TheWord) = "BATTLEFIELD" Then
        MsgBox "Correct"
    Else
        MsgBox "That is incorrect"
    End If
        
End Sub

Sub GetValueInputBox()

    Dim Monthly As String
    
    Monthly = InputBox("Enter your monthly salary:")
    
    If Len(Monthly) > 0 And IsNumeric(Monthly) Then
        MsgBox "Annualized: " & Monthly * 12
    Else
        MsgBox "Invalid input"
    End If

End Sub

_____________________________________________________________________

' Excel InputBox

Sub GetValueExcelInputBox()

    Dim Monthly As Variant
    
    Monthly = Application.InputBox _
        (Prompt:="Enter your monthly salary:", _
        Type:=1)
        
    If Monthly <> False Then
        MsgBox "Annulized: " & Monthly * 12
    End If
        
End Sub

Sub EraseRange()

    Dim UserRange As Range
    On Error Resume Next
        Set UserRange = Application.InputBox _
        (Prompt:="Select the range to erase:", _
        Title:="Range Erase", _
        Default:=Selection.Address, _
        Type:=8)
    On Error GoTo 0
    
    If Not UserRange Is Nothing Then
        UserRange.ClearContents
        UserRange.Select
    End If
    
End Sub