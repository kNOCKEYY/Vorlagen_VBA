Sub CopyRangeToPresentation()

    Dim PP As PowerPoint.Application
    Dim PPPres As PowerPoint.Presentation
    Dim PPSlide As PowerPoint.Slide
    Dim SlideTitle As String
    
    ' Open Powerpoint and create new presentation
    Set PP = New PowerPoint.Application
    Set PPPres = PP.Presentations.Add
    PP.Visible = True
    
    ' Add new slide as slide 1 and set focus on it
    Set PPSlide = PPPres.Slides.Add(1, ppLayoutTitleOnly)
    PPSlide.Select
    
    ' Copy the range as a picture
    Sheets("Slide Data").Range("A2:J28").CopyPicture _
        Appearance:=xlScreen, Format:=xlPicture
        
    ' Paste the picture and adjust its position
    PPSlide.Shapes.Paste.Select
    PP.ActiveWindow.Selection.ShapeRange.Align msoAlignCenters, True
    PP.ActiveWindow.Selection.ShapeRange.Align msoAlignMiddles, True
    
    ' Add the title to the slide
    SlideTitle = "My First Powerpoint Slide"
    PPSlide.Shapes.Title.TextFrame.TextRange.Text = SlideTitle
    
    ' Memory Cleanup
    PP.Activate
    Set PPSlide = Nothing
    Set PPPres = Nothing
    Set PP = Nothing

End Sub