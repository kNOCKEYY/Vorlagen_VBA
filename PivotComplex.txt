Sub CreateComplexPivotTable()

' Creates a new Sheet and PivotTable
' Works only on the first run after restard for some unknown reason

Dim PTCache As PivotCache
Dim PT As PivotTable

    Application.ScreenUpdating = False
    
    ' Delete PivotSheet if it exists
    On Error Resume Next
    Application.DisplayAlerts = False
    Sheets("PivotSheet").Delete
    On Error GoTo 0
    
    ' Delete PivotCache if it exists
    On Error Resume Next
    ActiveWorkbook.PivotCaches("BudgetPivotCache").Delete
    On Error GoTo 0

     ' Create Cache
    Set PTCache = ActiveWorkbook.PivotCaches.Create( _
        SourceType:=xlDatabase, _
        SourceData:=Range("A1").CurrentRegion)
        
    ' Add new worksheet
    Worksheets.Add
    ActiveSheet.Name = "PivotSheet"
    ActiveWindow.DisplayGridlines = False
    
    ' Create the pivot table
    Set PT = ActiveSheet.PivotTables.Add( _
        PivotCache:=PTCache, _
        TableDestination:=Range("A1"), _
        TableName:="BudgetPivot")
        
    With PT
        ' Filter
        .PivotFields("Category").Orientation = xlPageField
        .PivotFields("Division").Orientation = xlPageField
        ' Spalten
        .PivotFields("Month").Orientation = xlColumnField
        ' Zeilen
        .PivotFields("Department").Orientation = xlRowField
        ' Werte
        .PivotFields("Budget").Orientation = xlDataField
        .PivotFields("Actual").Orientation = xlDataField
        
        ' Change orientation to Rows because there's too much Data in Pivot
        .DataPivotField.Orientation = xlRowField
        
        ' Add a calculated field to compute variance
        .CalculatedFields.Add "Variance", "=Budget-Actual"
        .PivotFields("Variance").Orientation = xlDataField ' Filter
        
        ' Specify a number format
        .DataBodyRane.NumberFormat = "0,000"
        
        ' Apply a style
        .TableStyle2 = "PivotStyleMedium2"
        
        ' Hide Field Headers
        .DisplayFieldCaptions = False
        
        ' Change the captions
        .PivotFields("Sum of Budget").Caption = " Budget"
        .PivotFields("Sum of Actual").Caption = " Actual"
        .PivotFields("Sum of Variance").Caption = " Variance"
    End With

End Sub