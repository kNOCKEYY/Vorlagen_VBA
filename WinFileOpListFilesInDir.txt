Sub ListFilesInDir()

' List all files in a perticular directory with file size and date

    Dim Directory As String
    Dim r As Long
    Dim f As String
    Dim FileSize As Double
    
    ' String need an "\" at the end
    Directory = "D:\Excel\Excel 2019 Power Programming\"
    r = 1
    
    ' Insert headers
    Cells(r, 1) = "FileName"
    Cells(r, 2) = "Size"
    Cells(r, 3) = "Date/Time"
    Range("A1:C1").Font.Bold = True
    
    ' Get first file
    f = Dir(Directory, vbReadOnly + vbHidden + vbSystem)
    ' If you only want files of a specifc application:
    ' f = Dir(Directory & "*.xl??", vbReadOnly + vbHidden + vbSystem)
    
    Do While f <> ""
        r = r + 1
        Cells(r, 1) = f
        
        ' Adjust for filesize > 2 gigabytes
        FileSize = FileLen(Directory & f)
        If FileSize < 0 Then FileSize = FileSize + 4294967296#
        
        Cells(r, 2) = FileSize
        Cells(r, 3) = FileDateTime(Directory & f)
        
        ' Get next file
        f = Dir()
    Loop

End Sub